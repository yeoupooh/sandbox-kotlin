group 'com.subakstudio'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        jcenter()
//        maven { url "https://jitpack.io" }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = 'com.subakstudio.sandbox.htmlnotifier.HtmlNotifier'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.1"
    compile 'org.codehaus.groovy:groovy-all:2.3.11'

    // Telegram Bot API for Java
    compile 'com.github.pengrad:java-telegram-bot-api:1.3.2'
//    compile 'com.github.yeoupooh:java-telegram-bot-api:1.3.2'

    // Jackson
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'

    // OkHttp
    compile 'com.squareup.okhttp:okhttp:2.7.5'

    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'

    // Jsoup
    compile 'org.jsoup:jsoup:1.9.1'

    // RxJava
    compile 'io.reactivex:rxjava:1.1.3'

    // Apache Common IO
    compile 'commons-io:commons-io:2.5'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task createAllStartScripts() << {
    // just a placeholder
}
def scripts = ['CafeNotifier': 'com.subakstudio.sandbox.cafenotifier.CafeNotifier',
               'HtmlNotifier': 'com.subakstudio.sandbox.htmlnotifier.HtmlNotifier'
]
scripts.each() { scriptName, className ->
    def t = tasks.create(name: scriptName + 'StartScript', type: CreateStartScripts) {
        mainClassName = className
        applicationName = scriptName
        outputDir = new File(project.buildDir, 'scripts')
        classpath = jar.outputs.files + project.configurations.runtime
    }
    applicationDistribution.into("bin") {
        from(t)
        fileMode = 0755
    }
    createAllStartScripts.dependsOn(t)
}